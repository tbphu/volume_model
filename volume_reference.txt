function MAX(a, b)
  piecewise(a, a >= b, b);
end

function modulo(a, b)
  a - b*floor(a/b);
end

model volume_rheology

  // compartments
  compartment V_tot_liter;
  compartment extra = 1;
  
  V_os = 10//33.7;           // um^3
  V_b = 3.;  
  V_ref = V_os / ((pi_t * ((3 * V_os / (4 * pi) )^(1/3)) / (2 * d * E)) + 1);
  V_tot_liter := V_tot_fl * 1e-15;
  V_tot_fl := V_os + V_b;

 
  // species
  species c_e in extra = 240; // mM hat Bjoern gemessen (SD-F)
  species c_i in V_tot_liter = 322.2//317; // mM

  // internal and external osmotic
  pi_i    := c_i * R_G * T ;
  pi_e    := c_e * R_G * T ;
  
  // constants
  const R_G = 8.314; // J/mol/K
  T = 293; // K
  
  // geometric calculations
  G := (4*pi)^(1/3) * (3*V_tot_fl)^(2/3) // surface of sphere in um^2
  radius := (3/4/pi * V_tot_fl)^(1/3); // of shpere in um
  
  // water flow over membrane
  Lp = 1.19e-6; // um/s/Pa #Klipp 2005
  dV_os := -Lp * G * (pi_t + pi_e - pi_i); // water influx over the membrane -J_w
  V_os'  = dV_os;

  // growth of non osmotic volume
  dV_b = 0;
  //V_b' = dV_b; 
  dV_tot := dV_os + dV_b;
  
  // plastic expansion rate 
  pi_t0 = 0.202e-6
  pi_t = pi_t0*1e12; //0.200e6; // Pa
  pi_tc_0 = 2e5;
  d = 0.115; // um Goldenbogen, Giese 2016
  phi = 1.e-7; // 1/Pa/s
  pi_tc := pi_tc_0
  dV_b := 0.;
  V_ref' = phi*radius/2/d * MAX(pi_t - pi_tc, 0)*V_ref

  // turgor dynamics
  modulus_adjustment = 3.0; // TODO: check this !!!
  E = modulus_adjustment*2.58e6; // Pa Goldenbogen, Giese 2016    
  pi_t'  = E*2*d/radius * (dV_tot/V_ref) - E*phi* MAX(pi_t - pi_tc, 0) - pi_t/V_tot_fl * (dV_tot); 
  
  // nutrient uptake and consumptions
  k_nutrient = 2.5e-16; // mM/s/um^2   
  k_deg = 3e-16;
  => c_i; k_nutrient * G;
  c_i =>; k_deg*V_tot_fl;
end




